///////////////////////////////////////////////////////////////////////////////
//  GRADLE CONFIGURATION
///////////////////////////////////////////////////////////////////////////////

plugins {
  id("com.android.application")
  id("kotlin-android")
  id("kotlin-parcelize")
  kotlin("kapt")
  id("com.diffplug.spotless")
  id("org.jetbrains.dokka")
  signing
  `maven-publish`
}

///////////////////////////////////////////////////////////////////////////////
//  APP CONFIGURATION
///////////////////////////////////////////////////////////////////////////////

dependencies {
  // Demo common
  implementation("org.calypsonet.keyple:keyple-demo-ticketing-common-lib:2.1.0-SNAPSHOT") {
    isChanging = true
  }
  // Begin Keyple configuration (generated by
  // 'https://keyple.org/components/overview/configuration-wizard/')
  implementation("org.eclipse.keypop:keypop-reader-java-api:2.0.1")
  implementation("org.eclipse.keypop:keypop-calypso-card-java-api:2.1.2")
  implementation("org.eclipse.keypop:keypop-calypso-crypto-legacysam-java-api:0.7.0")
  implementation("org.eclipse.keypop:keypop-storagecard-java-api:0.2.0")
  implementation("org.eclipse.keyple:keyple-common-java-api:2.0.2")
  implementation("org.eclipse.keyple:keyple-plugin-storagecard-java-api:1.0.0")
  implementation("org.eclipse.keyple:keyple-util-java-lib:2.4.0")
  implementation("org.eclipse.keyple:keyple-service-java-lib:3.3.5")
  implementation("org.eclipse.keyple:keyple-card-calypso-java-lib:3.1.8")
  implementation("org.eclipse.keyple:keyple-card-calypso-crypto-legacysam-java-lib:0.9.0")
  implementation("org.eclipse.keyple:keyple-plugin-android-nfc-java-lib:3.1.0")
  // End Keyple configuration
  // Keyple reader plugins proprietary libs
  implementation(fileTree(mapOf("dir" to "libs", "include" to listOf("*.jar", "*.aar"))))
  // Other Keyple reader plugins
  implementation("org.calypsonet.keyple:keyple-plugin-cna-coppernic-cone2-java-lib:2.0.2")
  implementation("org.calypsonet.keyple:keyple-plugin-cna-famoco-se-communication-java-lib:2.0.2")
  // Android components
  implementation("androidx.appcompat:appcompat:1.6.1")
  implementation("com.google.android.material:material:1.10.0")
  implementation("androidx.constraintlayout:constraintlayout:2.1.4")
  implementation("androidx.activity:activity-ktx:1.8.1")
  implementation("androidx.fragment:fragment-ktx:1.6.2")
  implementation("androidx.multidex:multidex:2.0.1")
  // Kotlin
  implementation("androidx.core:core-ktx:1.12.0")
  implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.7.20")
  // Coroutines
  implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4")
  implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4")
  // Dagger dependencies
  kapt("com.google.dagger:dagger-compiler:2.19")
  annotationProcessor("com.google.dagger:dagger-compiler:2.19")
  kapt("com.google.dagger:dagger-android-processor:2.19")
  annotationProcessor("com.google.dagger:dagger-android-processor:2.19")
  implementation("com.google.dagger:dagger:2.19")
  implementation("com.google.dagger:dagger-android:2.19")
  implementation("com.google.dagger:dagger-android-support:2.19")
  compileOnly("org.glassfish:javax.annotation:10.0-b28")
  // Lottie
  implementation("com.airbnb.android:lottie:3.4.4")
  // Google GSON
  implementation("com.google.code.gson:gson:2.10.1")
  // Devnied - Byte Utils
  implementation("com.github.devnied:bit-lib4j:1.4.5") { exclude(group = "org.slf4j") }
  // Logging
  implementation("com.jakewharton.timber:timber:5.0.1")
  implementation("org.slf4j:slf4j-api:1.7.32")
  implementation("uk.uuid.slf4j:slf4j-android:1.7.32-0")
}

configurations.all { exclude(group = "com.arcao", module = "slf4j-timber") }

// dependencies {
//  // Demo common
//  implementation("org.calypsonet.keyple:keyple-demo-ticketing-common-lib:2.1.0-SNAPSHOT") {
// isChanging = true }
//
//  // Keyple reader plugins proprietary libs
//  implementation(fileTree(mapOf("dir" to "libs", "include" to listOf("*.jar", "*.aar"))))
//
//  // Begin Keyple configuration (generated by
// 'https://keyple.org/components/overview/configuration-wizard/')
//  implementation("org.eclipse.keypop:keypop-reader-java-api:2.0.1")
//  implementation("org.eclipse.keypop:keypop-calypso-card-java-api:2.1.2")
//  implementation("org.eclipse.keypop:keypop-calypso-crypto-legacysam-java-api:0.7.0")
//  implementation("org.eclipse.keypop:keypop-storagecard-java-api:0.2.0")
//  implementation("org.eclipse.keyple:keyple-common-java-api:2.0.2")
//  // implementation("org.eclipse.keyple:keyple-plugin-storagecard-java-api:1.0.0") // useless
// (transitively provided by Android NFC plugin)
//  implementation("org.eclipse.keyple:keyple-util-java-lib:2.4.0")
//  implementation("org.eclipse.keyple:keyple-service-java-lib:3.3.5")
//  implementation("org.eclipse.keyple:keyple-card-calypso-java-lib:3.1.8")
//  implementation("org.eclipse.keyple:keyple-card-calypso-crypto-legacysam-java-lib:0.9.0")
//  implementation("org.eclipse.keyple:keyple-plugin-android-nfc-java-lib:3.1.0")
//  // End Keyple configuration
//
//  // Other Keyple reader plugins
//  implementation("org.calypsonet.keyple:keyple-plugin-cna-coppernic-cone2-java-lib:2.0.2")
//  implementation("org.calypsonet.keyple:keyple-plugin-cna-famoco-se-communication-java-lib:2.0.2")
//
//  // Android components
//  implementation("androidx.appcompat:appcompat:1.6.1")
//  implementation("com.google.android.material:material:1.10.0")
//  implementation("androidx.constraintlayout:constraintlayout:2.1.4")
//  implementation("androidx.activity:activity-ktx:1.8.1")
//  implementation("androidx.fragment:fragment-ktx:1.6.2")
//
//  // Log
//  implementation("org.slf4j:slf4j-api:1.7.32")
//  implementation("com.jakewharton.timber:timber:5.0.1")
//  implementation("com.arcao:slf4j-timber:3.1@aar") //SLF4J binding for Timber
//
//  // Kotlin
//  implementation("androidx.core:core-ktx:1.12.0")
//  implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion")
//  implementation("org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion")
//
//  // Coroutines
//  implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4")
//  implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4")
//
//  implementation("androidx.multidex:multidex:2.0.1")
//
//  // RxJava
//  implementation("io.reactivex.rxjava2:rxjava:2.1.13")
//  implementation("io.reactivex.rxjava2:rxandroid:2.0.2")
//
//  // Google GSON
//  implementation("com.google.code.gson:gson:2.10.1")
//
//  // Devnied - Byte Utils
//  implementation("com.github.devnied:bit-lib4j:1.4.5") {
//    exclude(group = "org.slf4j")
//  }
//
//  // Dagger dependencies
//  kapt("com.google.dagger:dagger-compiler:2.19")
//  annotationProcessor("com.google.dagger:dagger-compiler:2.19")
//  kapt("com.google.dagger:dagger-android-processor:2.19")
//  annotationProcessor("com.google.dagger:dagger-android-processor:2.19")
//  implementation("com.google.dagger:dagger:2.19")
//  implementation("com.google.dagger:dagger-android:2.19")
//  implementation("com.google.dagger:dagger-android-support:2.19")
//  compileOnly("org.glassfish:javax.annotation:10.0-b28")
//
//  // Common lang
//  implementation("org.apache.commons:commons-lang3:3.11")
//
//  // Lottie
//  implementation("com.airbnb.android:lottie:3.4.4")
//
//  testImplementation("junit:junit:4.13.2")
//  testImplementation("org.robolectric:robolectric:4.3.1")
//  androidTestImplementation("com.android.support.test:runner:1.0.2")
//  androidTestImplementation("androidx.test.ext:junit:1.1.2")
//  androidTestImplementation("androidx.test.espresso:espresso-core:3.3.0")
//
//  coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:1.1.0")
// }

///////////////////////////////////////////////////////////////////////////////
//  STANDARD CONFIGURATION FOR ANDROID APPLICATION KOTLIN-BASED PROJECTS
///////////////////////////////////////////////////////////////////////////////

if (project.hasProperty("releaseTag")) {
  project.version = project.property("releaseTag") as String
  println("Release mode: version set to ${project.version}")
} else {
  println("Development mode: version is ${project.version}")
}

val title: String by project
val javaSourceLevel: String by project
val javaTargetLevel: String by project
val generatedOverviewFile = layout.buildDirectory.file("tmp/overview-dokka.md")

android {
  namespace = project.findProperty("androidAppNamespace") as String
  compileSdk = (project.findProperty("androidCompileSdk") as String).toInt()
  defaultConfig {
    applicationId = project.findProperty("androidAppId") as String
    minSdk = (project.findProperty("androidMinSdk") as String).toInt()
    versionCode = (project.findProperty("androidAppVersionCode") as String).toInt()
    versionName = project.findProperty("androidAppVersionName") as String
  }
  buildFeatures { viewBinding = true }
  buildTypes {
    getByName("release") {
      isMinifyEnabled = false
      proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
    }
  }
  compileOptions {
    sourceCompatibility = JavaVersion.toVersion(javaSourceLevel)
    targetCompatibility = JavaVersion.toVersion(javaTargetLevel)
    println("Compiling Java $sourceCompatibility to Java $targetCompatibility")
  }
  kotlinOptions { jvmTarget = javaTargetLevel }
  sourceSets {
    getByName("main").java.srcDirs("src/main/kotlin")
    getByName("debug").java.srcDirs("src/debug/kotlin")
  }
  packagingOptions {
    // Exclude 'META-INF/NOTICE.md' to resolve the conflict that occurs when multiple dependencies
    // include this file
    resources.excludes.add("META-INF/NOTICE.md")
  }
  applicationVariants.all {
    outputs.all {
      val outputImpl = this as com.android.build.gradle.internal.api.ApkVariantOutputImpl
      val variantName = name
      val versionName = project.version.toString()
      val newName = "${rootProject.name}-$versionName-$variantName.apk"
      outputImpl.outputFileName = newName
    }
  }
  publishing {
    singleVariant("release") {
      // No withSourcesJar() and withJavadocJar(), as we'll configure them manually during release
    }
  }
  lint { abortOnError = false }
}

fun copyLicenseFiles() {
  val metaInfDir = File(layout.buildDirectory.get().asFile, "resources/main/META-INF")
  val licenseFile = File(project.rootDir, "LICENSE")
  val noticeFile = File(project.rootDir, "NOTICE.md")
  metaInfDir.mkdirs()
  licenseFile.copyTo(File(metaInfDir, "LICENSE"), overwrite = true)
  noticeFile.copyTo(File(metaInfDir, "NOTICE.md"), overwrite = true)
}

tasks.withType<AbstractArchiveTask>().configureEach { archiveBaseName.set(rootProject.name) }

tasks {
  spotless {
    kotlin {
      target("src/**/*.kt")
      licenseHeaderFile("${project.rootDir}/LICENSE_HEADER")
      ktfmt()
    }
    kotlinGradle {
      target("**/*.kts")
      ktfmt()
    }
  }
  register("generateDokkaOverview") {
    outputs.file(generatedOverviewFile)
    doLast {
      val file = generatedOverviewFile.get().asFile
      file.parentFile.mkdirs()
      file.writeText(
          buildString {
            appendLine("# Module $title")
            appendLine()
            appendLine(
                file("src/main/kdoc/overview.md")
                    .takeIf { it.exists() }
                    ?.readText()
                    .orEmpty()
                    .trim())
            appendLine()
            appendLine("<br>")
            appendLine()
            appendLine("> ${project.findProperty("javadoc.copyright") as String}")
          })
    }
  }
  dokkaHtml.configure {
    dependsOn("generateDokkaOverview")
    dokkaSourceSets {
      named("main") {
        noAndroidSdkLink.set(false)
        includeNonPublic.set(false)
        includes.from(files(generatedOverviewFile))
        moduleName.set(title)
      }
    }
    doFirst { println("Generating Dokka HTML for ${project.name} version ${project.version}") }
  }
  withType<Jar>().configureEach {
    if (archiveClassifier.get() == "sources") {
      doFirst { copyLicenseFiles() }
      manifest {
        attributes(
            mapOf(
                "Implementation-Title" to "$title Sources",
                "Implementation-Version" to project.version))
      }
    }
  }
  register<Jar>("sourcesJar") {
    archiveClassifier.set("sources")
    from(android.sourceSets.getByName("main").java.srcDirs)
    from(layout.buildDirectory.dir("resources/main"))
    doFirst { copyLicenseFiles() }
    manifest {
      attributes(
          mapOf(
              "Implementation-Title" to "$title Documentation",
              "Implementation-Version" to project.version))
    }
  }
  register<Jar>("javadocJar") {
    dependsOn(dokkaHtml)
    archiveClassifier.set("javadoc")
    from(dokkaHtml.flatMap { it.outputDirectory })
    from(layout.buildDirectory.dir("resources/main"))
    doFirst { copyLicenseFiles() }
    manifest {
      attributes(
          mapOf(
              "Implementation-Title" to "$title Documentation",
              "Implementation-Version" to project.version))
    }
  }
  register("copyLicenseFiles") { doLast { copyLicenseFiles() } }
}

afterEvaluate {
  tasks.named("assembleRelease") { dependsOn("copyLicenseFiles") }
  publishing {
    publications {
      create<MavenPublication>("mavenJava") {
        from(components["release"])
        artifactId = rootProject.name
        artifact(tasks["sourcesJar"])
        artifact(tasks["javadocJar"])
        pom {
          name.set(project.findProperty("title") as String)
          description.set(project.findProperty("description") as String)
          url.set(project.findProperty("project.url") as String)
          licenses {
            license {
              name.set(project.findProperty("license.name") as String)
              url.set(project.findProperty("license.url") as String)
              distribution.set(project.findProperty("license.distribution") as String)
            }
          }
          developers {
            developer {
              name.set(project.findProperty("developer.name") as String)
              email.set(project.findProperty("developer.email") as String)
            }
          }
          organization {
            name.set(project.findProperty("organization.name") as String)
            url.set(project.findProperty("organization.url") as String)
          }
          scm {
            connection.set(project.findProperty("scm.connection") as String)
            developerConnection.set(project.findProperty("scm.developerConnection") as String)
            url.set(project.findProperty("scm.url") as String)
          }
          ciManagement {
            system.set(project.findProperty("ci.system") as String)
            url.set(project.findProperty("ci.url") as String)
          }
          properties.set(
              mapOf(
                  "project.build.sourceEncoding" to "UTF-8",
                  "maven.compiler.source" to javaSourceLevel,
                  "maven.compiler.target" to javaTargetLevel))
        }
      }
    }
    repositories {
      maven {
        if (project.hasProperty("sonatypeURL")) {
          url = uri(project.property("sonatypeURL") as String)
          credentials {
            username = project.property("sonatypeUsername") as String
            password = project.property("sonatypePassword") as String
          }
        }
      }
    }
  }
  signing {
    if (project.hasProperty("releaseTag")) {
      useGpgCmd()
      sign(publishing.publications["mavenJava"])
    }
  }
}
